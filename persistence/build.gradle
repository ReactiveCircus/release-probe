plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

kapt {
    useBuildCache = true
    mapDiagnosticLocations = true
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testApplicationId 'reactivecircus.releaseprobe.persistence.test'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"]
            }
        }

        // Room database
        buildConfigField "String", "DATABASE_NAME", "\"release-probe.db\""
        buildConfigField "int", "DATABASE_SCHEMA_VERSION", "1"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            testCoverageEnabled enableCoverage
        }
    }

    testOptions {
        animationsDisabled = true
    }

    packagingOptions {
        pickFirst 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    // rx
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxJava}"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx.coroutines}"

    // Koin
    implementation "org.koin:koin-android:${versions.koin}"

    // AndroidX
    implementation "androidx.annotation:annotation:${versions.androidx.annotation}"
    implementation "androidx.arch.core:core-runtime:${versions.androidx.arch}"
    implementation "androidx.room:room-runtime:${versions.androidx.room}"
    kapt "androidx.room:room-compiler:${versions.androidx.room}"
    implementation "androidx.room:room-ktx:${versions.androidx.room}"
    implementation "androidx.room:room-rxjava2:${versions.androidx.room}"

    // timber
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // Unit tests
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.amshove.kluent:kluent-android:${versions.kluent}"

    // Android tests
    androidTestImplementation "androidx.test:core-ktx:${versions.androidx.test.core}"
    androidTestImplementation "androidx.test:monitor:${versions.androidx.test.monitor}"
    androidTestImplementation "androidx.test:runner:${versions.androidx.test.runner}"
    androidTestImplementation "androidx.test:rules:${versions.androidx.test.rules}"
    androidTestImplementation "androidx.test.ext:junit-ktx:${versions.androidx.test.ext.junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.androidx.espresso}"
    androidTestImplementation "androidx.arch.core:core-testing:${versions.androidx.arch}"
    androidTestImplementation "androidx.room:room-testing:${versions.androidx.room}"
    androidTestImplementation "org.amshove.kluent:kluent-android:${versions.kluent}"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlinx.coroutines}"
}
