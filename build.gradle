buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"

    ext.buildConfig = [
            minSdk: 22,
            targetSdk: 28,
            compileSdk: 28,
            buildTools: '28.0.2'
    ]

    ext.versions = [
            androidGradlePlugin: '3.3.0-alpha05',
            googleServicesGradlePlugin: '4.0.1',
            dexcountGradlePlugin: '0.8.3',
            bugsnag: '4.6.0',
            detekt: '1.0.0.RC8',
            leakCanary: '1.6.1',
            kotlin: '1.2.60',
            kotlinCoroutines: '0.24.0',
            firebase: [
                    core: '16.0.1'
            ],
            androidx: [
                    core: '1.0.0-rc01',
                    annotation: '1.0.0-rc01',
                    appCompat: '1.0.0-rc01',
                    fragment: '1.0.0-rc01',
                    coordinatorLayout: '1.0.0-rc01',
                    constraintLayout: '1.1.2',
                    arch: '2.0.0-rc01',
                    lifecycle: '2.0.0-rc01',
                    room: '2.0.0-rc01',
                    paging: '2.0.0-rc01',
                    navigation: '1.0.0-alpha04',
                    work: '1.0.0-alpha06',
                    test: '1.1.0-alpha4',
                    espresso: '3.1.0-alpha4'
            ],
            material: '1.0.0-rc01',
            okhttp: '3.11.0',
            retrofit: '2.4.0',
            moshi: '1.6.0',
            rxJava: '2.2.0',
            rxKotlin: '2.2.0',
            rxAndroid: '2.0.2',
            rxLint: '1.6.1',
            dagger: '2.16',
            timber: '4.7.1',
            store: '3.1.0',
            glide: '4.7.1',
            junit: '4.12',
            mockk: '1.8.6',
            kluent: '1.40'
    ]

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServicesGradlePlugin}"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${versions.dexcountGradlePlugin}"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:${versions.androidx.navigation}"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(Test) {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

subprojects {
    apply from: "$project.rootDir/gradle/detekt.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def envOrProp(String name) {
    def value = System.getenv(name)
    value != null ? value : hasProperty(name) ? getProperty(name) : ''
}
