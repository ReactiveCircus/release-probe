buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"
    ext.isIdeBuild = project.hasProperty('android.injected.invoked.from.ide')
    ext.enableCoverage = project.hasProperty('coverage')

    ext.buildConfig = [
            minSdk    : 23,
            targetSdk : 29,
            compileSdk: 29,
            buildTools: '29.0.1'
    ]

    ext.versions = [
            androidGradlePlugin       : '3.6.0-alpha05',
            googleServicesGradlePlugin: '4.3.0',
            dexcountGradlePlugin      : '0.8.6',
            bugsnag                   : '4.17.1',
            detekt                    : '1.0.0-rework-beta5',
            leakCanary                : '2.0-beta-1',
            kotlin                    : '1.3.41',
            kotlinx                   : [
                    coroutines: '1.3.0-RC',
            ],
            firebase                  : [
                    core: '17.0.1'
            ],
            blueprint                 : '1.0.0-SNAPSHOT',
            androidx                  : [
                    core              : '1.2.0-alpha02',
                    annotation        : '1.1.0',
                    appCompat         : '1.1.0-rc01',
                    activity          : '1.1.0-alpha01',
                    fragment          : '1.2.0-alpha01',
                    coordinatorLayout : '1.1.0-beta01',
                    recyclerView      : '1.1.0-beta01',
                    swipeRefreshLayout: '1.1.0-alpha02',
                    constraintLayout  : '1.1.3',
                    arch              : '2.1.0-rc01',
                    lifecycle         : '2.2.0-alpha02',
                    room              : '2.2.0-alpha01',
                    paging            : '2.1.0',
                    navigation        : '2.1.0-beta02',
                    work              : '2.2.0-rc01',
                    test              : [
                            core   : '1.2.1-alpha02',
                            monitor: '1.3.0-alpha02',
                            rules  : '1.3.0-alpha02',
                            runner : '1.3.0-alpha02',
                            ext    : [
                                    junit: '1.1.2-alpha02'
                            ]
                    ],
                    espresso          : '3.3.0-alpha02'
            ],
            material                  : '1.1.0-alpha09',
            okhttp                    : '4.0.1',
            retrofit                  : '2.6.0',
            moshi                     : '1.8.0',
            json                      : '20180130',
            rxJava                    : '2.2.10',
            koin                      : '2.0.1',
            timber                    : '4.7.1',
            store                     : '3.1.1',
            junit                     : '4.12',
            mockk                     : '1.9.3',
            kluent                    : '1.53'
    ]

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServicesGradlePlugin}"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${versions.dexcountGradlePlugin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.androidx.navigation}"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all Kotlin stdlib artifacts to use the same version.
                if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name.startsWith('kotlin-stdlib')) {
                    details.useVersion versions.kotlin
                }
            }
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            freeCompilerArgs += [
                    '-progressive',
                    '-XXLanguage:+NewInference',
                    '-XXLanguage:+InlineClasses',
                    '-Xuse-experimental=kotlin.Experimental',
            ]
        }
    }

    tasks.withType(Test).configureEach {
        maxParallelForks Runtime.runtime.availableProcessors() * 2
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

subprojects {
    apply from: "$project.rootDir/gradle/detekt.gradle"

    /**
     * Disable debug and mock tests to avoid running the same tests repeatedly in different build variants.
     * ./gradlew test -PslimTests will run unit tests for *ProdRelease and *Release in Android App and Library projects,
     * and all tests in JVM projects.
     */
    if (project.hasProperty('slimTests')) {
        tasks.whenTaskAdded { task ->
            if (task.name ==~ /test.*(?i)(debug|mock).*/) {
                task.configure {
                    it.enabled = false
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

def envOrProp(String name) {
    def value = System.getenv(name)
    value != null ? value : project.hasProperty(name) ? project.getProperty(name) : ''
}

apply from: "$project.rootDir/gradle/projectDependencyGraph.gradle"
