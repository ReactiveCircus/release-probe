buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"

    ext.buildConfig = [
            minSdk    : 23,
            targetSdk : 28,
            compileSdk: 28,
            buildTools: '28.0.3'
    ]

    ext.versions = [
            androidGradlePlugin       : '3.4.0-alpha08',
            googleServicesGradlePlugin: '4.2.0',
            dexcountGradlePlugin      : '0.8.4',
            bugsnag                   : '4.9.3',
            detekt                    : '1.0.0-RC12',
            leakCanary                : '1.6.2',
            kotlin                    : '1.3.11',
            firebase                  : [
                    core: '16.0.6'
            ],
            androidx                  : [
                    core             : '1.1.0-alpha02',
                    coreKtx          : '1.0.1',
                    annotation       : '1.0.1',
                    appCompat        : '1.1.0-alpha01',
                    activity         : '1.0.0-alpha02',
                    fragment         : '1.1.0-alpha02',
                    coordinatorLayout: '1.1.0-alpha01',
                    recyclerView     : '1.1.0-alpha01',
                    constraintLayout : '1.1.3',
                    arch             : [
                            coreRuntime: '2.0.1-alpha01',
                            coreTesting: '2.0.0',
                    ],
                    lifecycle        : '2.0.0',
                    room             : '2.1.0-alpha03',
                    paging           : '2.1.0-rc01',
                    navigation       : '1.0.0-alpha08',
                    work             : '1.0.0-alpha13',
                    test             : [
                            core   : '1.1.0',
                            monitor: '1.1.1',
                            rules  : '1.1.1',
                            runner : '1.1.1',
                            ext    : [
                                    junit: '1.1.0'
                            ]
                    ],
                    espresso         : '3.1.1'
            ],
            material                  : '1.1.0-alpha02',
            okhttp                    : '3.12.0',
            retrofit                  : '2.5.0',
            moshi                     : '1.8.0',
            json                      : '20180130',
            rxJava                    : '2.2.3',
            rxKotlin                  : '2.3.0',
            rxAndroid                 : '2.1.0',
            rxLint                    : '1.7.1',
            koin                      : '1.0.2',
            timber                    : '4.7.1',
            store                     : '3.1.0',
            junit                     : '4.12',
            mockk                     : '1.8.13.kotlin13',
            kluent                    : '1.44'
    ]

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServicesGradlePlugin}"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${versions.dexcountGradlePlugin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:${versions.androidx.navigation}"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += ["-progressive"]
        }
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

subprojects {
    apply from: "$project.rootDir/gradle/detekt.gradle"
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

def envOrProp(String name) {
    def value = System.getenv(name)
    value != null ? value : hasProperty(name) ? getProperty(name) : ''
}

apply from: "$project.rootDir/gradle/projectDependencyGraph.gradle"
