buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"
    ext.isIdeBuild = project.hasProperty('android.injected.invoked.from.ide')
    ext.enableCoverage = project.hasProperty('coverage')

    ext.buildConfig = [
            minSdk    : 23,
            targetSdk : 29,
            compileSdk: 29,
            buildTools: '29.0.2'
    ]

    ext.versions = [
            androidGradlePlugin       : '4.0.0-alpha07',
            googleServicesGradlePlugin: '4.3.3',
            dexcountGradlePlugin      : '1.0.2',
            bugsnag                   : '4.21.1',
            detekt                    : '1.2.2',
            leakCanary                : '2.0',
            kotlin                    : '1.3.61',
            kotlinx                   : [
                    coroutines: '1.3.3',
            ],
            firebase                  : [
                    core: '17.2.1'
            ],
            blueprint                 : '1.3.1',
            flowbinding               : '0.6.0',
            androidx                  : [
                    core              : '1.2.0-rc01',
                    annotation        : '1.1.0',
                    appCompat         : '1.2.0-alpha01',
                    activity          : '1.1.0-rc03',
                    fragment          : '1.2.0-rc04',
                    coordinatorLayout : '1.1.0',
                    recyclerView      : '1.2.0-alpha01',
                    swipeRefreshLayout: '1.1.0-alpha03',
                    constraintLayout  : '1.1.3',
                    arch              : '2.1.0',
                    lifecycle         : '2.2.0-rc03',
                    room              : '2.2.3',
                    paging            : '2.1.1',
                    navigation        : '2.2.0-rc04',
                    work              : '2.3.0-beta02',
                    test              : [
                            core   : '1.3.0-alpha03',
                            monitor: '1.3.0-alpha03',
                            rules  : '1.3.0-alpha03',
                            runner : '1.3.0-alpha03',
                            ext    : [
                                    junit: '1.1.2-alpha03'
                            ]
                    ],
                    espresso          : '3.3.0-alpha03'
            ],
            material                  : '1.2.0-alpha03',
            okhttp                    : '4.2.2',
            retrofit                  : '2.7.0',
            moshi                     : '1.9.2',
            json                      : '20190722',
            rxJava                    : '2.2.16',
            koin                      : '2.0.1',
            timber                    : '4.7.1',
            store                     : '3.1.1',
            junit                     : '4.12',
            mockk                     : '1.9.3',
            kluent                    : '1.58'
    ]

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServicesGradlePlugin}"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${versions.dexcountGradlePlugin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    configurations.all {
        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-anr"
        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-ndk"
    }

    plugins.withType(com.android.build.gradle.LibraryPlugin) {
        // TODO disable buildConfig once androidTest works without it
        project.android.buildFeatures.buildConfig = true
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        plugin.extension.packagingOptions {
            exclude 'kotlin/**'
            exclude '**/*.kotlin_metadata'
            exclude 'META-INF/*.kotlin_module'
            exclude 'META-INF/*.version'
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            freeCompilerArgs += [
                    '-progressive',
                    '-XXLanguage:+NewInference',
                    '-XXLanguage:+InlineClasses',
                    '-Xuse-experimental=kotlin.Experimental',
            ]
        }
    }

    tasks.withType(Test).configureEach {
        maxParallelForks Runtime.runtime.availableProcessors() * 2
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

subprojects {
    apply from: "$project.rootDir/gradle/detekt.gradle"

    /**
     * Disable debug and mock tests to avoid running the same tests repeatedly in different build variants.
     * ./gradlew test -PslimTests will run unit tests for *ProdRelease and *Release in Android App and Library projects,
     * and all tests in JVM projects.
     */
    if (project.hasProperty('slimTests')) {
        tasks.whenTaskAdded { task ->
            if (task.name ==~ /test.*(?i)(debug|mock).*/) {
                task.configure {
                    it.enabled = false
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

def envOrProp(String name) {
    def value = System.getenv(name)
    value != null ? value : project.hasProperty(name) ? project.getProperty(name) : ''
}

apply from: "$project.rootDir/gradle/projectDependencyGraph.gradle"
