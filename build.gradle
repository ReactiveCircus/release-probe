buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"
    ext.isIdeBuild = project.hasProperty('android.injected.invoked.from.ide')

    ext.buildConfig = [
            minSdk    : 23,
            targetSdk : 28,
            compileSdk: 28,
            buildTools: '28.0.3'
    ]

    ext.versions = [
            androidGradlePlugin       : '3.5.0-alpha06',
            googleServicesGradlePlugin: '4.2.0',
            dexcountGradlePlugin      : '0.8.6',
            bugsnag                   : '4.12.0',
            detekt                    : '1.0.0-RC14',
            leakCanary                : '1.6.3',
            kotlin                    : '1.3.21',
            firebase                  : [
                    core: '16.0.7'
            ],
            androidx                  : [
                    core             : '1.1.0-alpha04',
                    annotation       : '1.0.2',
                    appCompat        : '1.1.0-alpha02',
                    activity         : '1.0.0-alpha04',
                    fragment         : '1.1.0-alpha04',
                    coordinatorLayout: '1.1.0-alpha01',
                    recyclerView     : '1.1.0-alpha02',
                    constraintLayout : '1.1.3',
                    arch             : [
                            coreRuntime: '2.0.1-alpha01',
                            coreTesting: '2.0.0',
                    ],
                    lifecycle        : '2.1.0-alpha02',
                    room             : '2.1.0-alpha04',
                    paging           : '2.1.0',
                    navigation       : '2.0.0-rc02',
                    work             : '2.0.0-rc01',
                    test             : [
                            core   : '1.1.1-alpha01',
                            monitor: '1.1.2-alpha01',
                            rules  : '1.1.2-alpha01',
                            runner : '1.1.2-alpha01',
                            ext    : [
                                    junit: '1.1.1-alpha01'
                            ]
                    ],
                    espresso         : '3.1.2-alpha01'
            ],
            material                  : '1.1.0-alpha04',
            okhttp                    : '3.13.1',
            retrofit                  : '2.5.0',
            moshi                     : '1.8.0',
            json                      : '20180130',
            rxJava                    : '2.2.7',
            rxKotlin                  : '2.3.0',
            rxAndroid                 : '2.1.1',
            rxLint                    : '1.7.3',
            koin                      : '1.0.2',
            timber                    : '4.7.1',
            store                     : '3.1.0',
            junit                     : '4.12',
            mockk                     : '1.9.1',
            kluent                    : '1.48'
    ]

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServicesGradlePlugin}"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${versions.dexcountGradlePlugin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.androidx.navigation}"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += ["-progressive"]
        }
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

subprojects {
    apply from: "$project.rootDir/gradle/detekt.gradle"
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

def envOrProp(String name) {
    def value = System.getenv(name)
    value != null ? value : project.hasProperty(name) ? project.getProperty(name) : ''
}

apply from: "$project.rootDir/gradle/projectDependencyGraph.gradle"

/**
 * Disable debug and mock tests to avoid running the same tests repeatedly in different build variants.
 * ./gradlew test -PslimTests will run unit tests for *ProdRelease and *Release in Android App and Library projects,
 * and all tests in JVM projects.
 */
gradle.taskGraph.whenReady { graph ->
    if (project.hasProperty('slimTests')) {
        graph.allTasks.findAll { it.name ==~ /test.*(?i)(debug|mock).*/ }*.enabled = false
    }
}
